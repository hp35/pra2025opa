charlist oct"122": oct"132";

input cmbase;
def generate= enddef;
input cmex10

math_axis#:=216/36pt#;

mode_setup; font_setup;

%input cmbase % te.960730
%input mathex
%input bigdel
%input bigop 

cmchar "\textstyle contour double integral sign";
beginchar(oct"122",18u#,0,100/54dh#); % width single integral: 12u\#
italcorr 3.5u#; % \TeX\ kerning between upper limit and lower limit
adjust_fit(0,-ic#); pickup fine.nib;
w:=w*2/3; % proportions shall be the same as for the single integral
pos1(curve,0); pos2(hair,0); pos3(vair,90); pos4(stem,180);
pos5(curve,180); pos5'(curve,0); z5'=z5; pos6(stem,0);
pos7(vair,-90); pos8(hair,-180); pos9(curve,-180);
rt x1r=hround(w-u); x9=w-x1; x7=w-x3=3u;
x5=.5[x4,x6]; x4-x6=1.2u; lft x5r=hround(.5w-.5stem);
top y3r=0; bot y7r=-d; y9-.5curve=vround(top y7l+.25curve);
y3-y1=y9-y7; y5=.5[y3,y7]=.5[y4,y6]; y4-y6=.6(y3-y7);
bulb(3,2,1); bulb(7,8,9);  % bulbs
filldraw stroke z3e{left}...z4e{(z5e-z4e)xscaled 1.1}
 ..tension atleast 1 and atleast .8..{z5-z4}z5e;  % upper stem
filldraw stroke z5'e{z6-z5}..tension atleast.8 and atleast 1
 ..{(z6e-z5'e)xscaled 1.1}z6e...{left}z7e;  % lower stem
picture intpicture; intpicture := currentpicture;
clearit;
currentpicture := currentpicture + (intpicture shifted(-w/4,0));
currentpicture := currentpicture + (intpicture shifted(w/4,0));
pickup rule.nib; autorounded;
draw fullcircle xscaled 1.1w yscaled 0.7w shifted z5;
% now get the position right
currentpicture := currentpicture shifted(w/4,0);
penlabels(1,2,3,4,5,6,7,8,9); endchar;

"\displaystyle contour double integral sign";
beginchar(oct"132",27u#,0,200/54dh#); %width single integral: 18u\#
italcorr 8u#; % \TeX\ kerning between upper limit and lower limit
adjust_fit(0,-ic#); pickup fine.nib;
numeric bulb_size, max_size;
w:=w*2/3; %proportions shall be the same as for the single integral
bulb_size=hround(bold+dw); max_size=hround(bold+2dw);
pos1(bulb_size,0); pos2(hair,0); pos3(vair,90); pos4(bold,180);
pos5(max_size,180); pos5'(max_size,0); z5'=z5; pos6(bold,0);
pos7(vair,-90); pos8(hair,-180); pos9(bulb_size,-180);
rt x1r=hround(w-u); x9=w-x1; x7=w-x3=3u;
x5=.5[x4,x6]; x4-x6=4.8u; lft x5r=hround(.5w-.5stem);
top y3r=0; bot y7r=-d;
y9-.5bulb_size=vround(top y7l+.25bulb_size); y3-y1=y9-y7;
y5=.5[y3,y7]=.5[y4,y6]; y4-y6=.6(y3-y7);
bulb(3,2,1); bulb(7,8,9);  % bulbs
filldraw stroke z3e{left}...z4e{(z5e-z4e)xscaled 1.1}
 ..tension atleast 1 and atleast .8..{z5-z4}z5e;  % upper stem
filldraw stroke z5'e{z6-z5}..tension atleast.8 and atleast 1
 ..{(z6e-z5'e)xscaled 1.1}z6e...{left}z7e;  % lower stem
penlabels(1,2,3,4,5,6,7,8,9); 
picture intpicture; intpicture := currentpicture;
clearit;
currentpicture := currentpicture + (intpicture shifted(-w/4,0));
currentpicture := currentpicture + (intpicture shifted(w/4,0));
pickup pencircle scaled 1.2rule_thickness; autorounded;
%pickup rule.nib; autorounded;
draw fullcircle xscaled 1.2w yscaled 0.8w shifted z5;
% now get the position right
currentpicture := currentpicture shifted(w/4,0);
endchar;

font_x_height x_height#;
font_quad 18u#+4letter_fit#;

bye
